plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
	id "forgified-fabric-loom" version "0.6.54"
	id "java"
	id "idea"
}

def ENV = System.getenv()

archivesBaseName = project.archives_base_name
version = "${mod_version}.${ENV.BUILD_NUMBER ?: "custom"}"
group = project.maven_group

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

loom {
	silentMojangMappingsLicense()
    useFabricMixin = true
}

repositories {
	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
}

dependencies {
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings(minecraft.officialMojangMappings())
	
    forge("net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}")

    modCompile("curse.maven:kubejs-238086:3185095")
    modCompile("curse.maven:rhino-416294:3186772")
    modCompile("curse.maven:architectury-forge-419699:3183867")
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"

	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		options.release = targetVersion
	}
}